import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-parcelize'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

def Properties props = new Properties()
def propFile = rootProject.file('./sign/keystore.properties')   //pay attention to the path
def vkeyStore
def vstorePassword
def vkeyAlias
def vkeyPassword
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('keyStore')) {
        vkeyStore = props['keyStore']
    }

    if (props != null && props.containsKey('storePassword')) {
        vstorePassword = props['storePassword']
    }

    if (props != null && props.containsKey('keyAlias')) {
        vkeyAlias = props['keyAlias']
    }

    if (props != null && props.containsKey('keyPassword')) {
        vkeyPassword = props['keyPassword']
    }
}


android {
    signingConfigs {
        config {
            keyAlias vkeyAlias
            keyPassword vkeyPassword
            storeFile rootProject.file(vkeyStore)
            storePassword vstorePassword
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.mario.androidpacksample"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true // add this to enable multi-dex
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }

        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

//    flavorDimensions "default"
//    productFlavors {
//        angtalk {
//            dimension "default"
//
//            applicationId "com.maximko.angtalk"
//            versionCode 6
//            versionName "1.0.3"
//
//            resValue "string", "app_name", "즐팅♡방"
//
//            buildConfigField 'String', 'APP_ID', '"angtalk"'
//            //buildConfigField 'String', 'SERVER_URL', '"http://192.168.0.10:9969/api/"'
//            buildConfigField 'String', 'SERVER_URL', '"https://ec2-52-198-73-199.ap-northeast-1.compute.amazonaws.com/index.php/angtalk/"'
//            buildConfigField 'boolean', 'IS_TEST', 'true'
//        }
//    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        disable 'NullSafeMutableLiveData'
    }

    applicationVariants.all { variant ->
        if (!variant.buildType.name.equals("debug")) {
            def df = new SimpleDateFormat("yyMMddhhmmss")
            df.setTimeZone(TimeZone.getDefault())
            variant.outputs.all { output ->
                outputFileName = new File("../", "template" + "_v" + defaultConfig.versionName + "_" + df.format(new Date()) + ".apk")
            }
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.21"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // architecture
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation "androidx.work:work-runtime-ktx:2.6.0-rc01"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"

    // ui
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    //// navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"
    //// swipe
    implementation "com.daimajia.swipelayout:library:1.2.0@aar"
    implementation 'me.relex:circleindicator:2.1.4'

    // local db
    implementation "androidx.room:room-runtime:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"

    // image
    implementation "com.github.bumptech.glide:glide:4.11.0"
    annotationProcessor "com.github.bumptech.glide:compiler:4.11.0"
    api 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    // network
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.zhy:okhttputils:2.6.2'

    // paging
    implementation "androidx.paging:paging-runtime-ktx:3.0.1"

    // ble
    implementation 'com.akexorcist:bluetoothspp:1.0.0'
    implementation 'com.clj.fastble:FastBleLib:2.3.4'

    // dagger-hilt
    implementation 'com.google.dagger:dagger:2.38.1'
    kapt 'com.google.dagger:dagger-compiler:2.38.1'

    // fcm push
    implementation "com.google.firebase:firebase-core:19.0.0"
    implementation "com.google.firebase:firebase-messaging:22.0.0"

    // inapp 4.0.0
    implementation "com.android.billingclient:billing:4.0.0"

    // google map
    implementation 'com.google.android.gms:play-services-basement:17.6.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.maps.android:android-maps-utils:2.2.0'

    // log
    implementation 'com.orhanobut:logger:2.2.0'
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation 'com.android.installreferrer:installreferrer:2.2'

    // etc
    implementation "com.google.guava:guava:24.1-jre"
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.blankj:utilcode:1.30.0'

    implementation project(':libMBase')
    //implementation(name:'libpicaon', ext:'aar')
}
apply plugin: 'com.google.gms.google-services'